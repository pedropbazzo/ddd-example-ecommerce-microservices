plugins {
    id 'org.springframework.boot' version "${springBoot}" apply false
    id 'io.spring.dependency-management' version "${dependencyManagement}"
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'io.spring.dependency-management'

    group = 'com.ttulka.ecommerce.sales'
    version = '2.0-SNAPSHOT'

    sourceCompatibility = 11
    targetCompatibility = 11

    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    javadoc {
        if (JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }

    test {
        useJUnitPlatform()
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = 'catalog-' + project.name
                from components.java
                versionMapping {
                    usage('java-api') {
                        fromResolutionOf('runtimeClasspath')
                    }
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }
            }
        }
    }
}

task build {
    dependsOn ':domain:build'
    dependsOn ':jdbc:build'
    dependsOn ':spring-boot-starter:build'
}